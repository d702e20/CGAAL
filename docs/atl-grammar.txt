start := phi

phi := paren |
        proposition |
        not |
        or |
        and |
        bool |
        enforce_next |
        enforce_until |
        enforce_eventually |
        enforce_invariant |
        despite_next |
        despite_until |
        despite_eventually |
        despite_invariant

paren := "(" phi ")"

enforce_players := "<<" players ">>"
despite_players := "[[" players "]]"

next := "X" phi
until := "(" phi "U" phi ")"
eventually := "F" phi
invariant := "G" phi

enforce_next := enforce_players next
enforce_until := enforce_players until
enforce_eventually := enforce_players eventually
enforce_invariant := enforce_players invariant

despite_next := despite_players next
despite_until := despite_players until
despite_eventually := despite_players eventually
despite_invariant := despite_players invariant

proposition := "'" identifier "'"
not := "!" phi
or := phi "|" phi
and := phi "&" phi

bool := "true" | "false"
players := identifier | identifier "," players
identifier := [a-zA-Z0-9]+