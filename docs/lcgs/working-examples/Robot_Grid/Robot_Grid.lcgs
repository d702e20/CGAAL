
const N = 3;

player r1 = robot [START_X=1, START_Y=1, TARGET_X=N, TARGET_Y=N, OTHER_1=r2, OTHER_2=r3, OTHER_3=r4];
player r2 = robot [START_X=N, START_Y=N, TARGET_X=1, TARGET_Y=1, OTHER_1=r1, OTHER_2=r3, OTHER_3=r4];
player r3 = robot [START_X=1, START_Y=N, TARGET_X=N, TARGET_Y=1, OTHER_1=r1, OTHER_2=r2, OTHER_3=r4];
player r4 = robot [START_X=N, START_Y=1, TARGET_X=1, TARGET_Y=N, OTHER_1=r1, OTHER_2=r2, OTHER_3=r3];

label any_crashes = r1.crash || r2.crash || r3.crash || r4.crash;

template robot
    x : [1 .. N] init START_X;
    x' = min(N, max(1, x
        + move_right
        - move_left
    ));

    y : [1 .. N] init START_Y;
    y' = min(N, max(1, y
         + move_up
         - move_down
    ));

    crash : [0..1] init 0;
    crash' = crash
                || (x == OTHER_1.x && y == OTHER_1.y)
                || (x == OTHER_2.x && y == OTHER_2.y)
                || (x == OTHER_3.x && y == OTHER_3.y);

    label at_start = x == START_X && y == START_Y;
    label at_target = x == TARGET_X && y == TARGET_Y;

    [wait] 1;
    [move_up] !crash && y < N;
    [move_down] !crash && y > 1;
    [move_right] !crash && x < N;
    [move_left] !crash && x > 1;
endtemplate
