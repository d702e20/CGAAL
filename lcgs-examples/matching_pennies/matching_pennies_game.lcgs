// Matching Pennies game
// Players are either even or odd, they both have a penny,
// if the sum of the number of heads is even, even wins, otherwise odd wins.

const winning_sum = 100;

player odd = penny_player[other=even, is_odd=1];
player even = penny_player[other=odd, is_odd=0];

label game_over = odd.sum >= winning_sum || even.sum >= winning_sum;
label odd_has_largest_sum = odd.sum >= even.sum;

latest_winner : [-1..1] init -1;
latest_winner' = odd.head * even.tail + odd.tail * even.head;

label odd_won_round = latest_winner == 1;
label even_won_round = latest_winner == 0;

template penny_player
    sum : [0..winning_sum] init 50;
    // Calculate player's new sum (sum + win? - lose?)
    sum' = is_odd ?
                  sum + (other.head * tail + other.tail * head) - (other.head * head + other.tail * tail)
                : sum + (other.head * head + other.tail * tail) - (other.head * tail + other.tail * head);

    // players can continue to take head or tail transitions as long as neither has reached winning_sum
    [head] sum < winning_sum && other.sum < winning_sum;
    [tail] sum < winning_sum && other.sum < winning_sum;
    [wait] sum == winning_sum || other.sum == winning_sum;
endtemplate
